<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>WegGL Example</title>
    <script type="text/javascript" src="gl-matrix-min.js"></script>
    <script type="text/javascript" src="main.js"></script>
    <script type="text/javascript" src="Shape.js"></script>
    <script type="text/javascript" src="helpers.js"></script>
    <script type="text/javascript" src="globals.js"></script>
    <script type="text/javascript" src="ShaderProgram.js"></script>
    <script type="text/javascript" src="interactive_keys.js"></script>
    <script type="text/javascript" src="shape_creator.js"></script>
    <script type="text/javascript" src="teapot.js"></script>
    <script type="text/javascript" src="Light.js"></script>
    <!-- VSHADER WITHOUT ANY LIGHT -->
    <script id="v-shader-nolight" type="x-shader/x-vertex"> precision mediump float;

        attribute vec3 vertexPosition; 
        attribute vec3 vertexColor;   

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;

        varying vec3 fragmentColor;    

        void main() {
            gl_Position = projectionMatrix * modelViewMatrix * vec4(vertexPosition, 1.0); 
            fragmentColor = vertexColor; 
        }
    </script>
    <!-- GOURARD DIFFUSE VSHADER -->
    <script id="v-shader" type="x-shader/x-vertex"> precision mediump float;

        attribute vec3 vertexPosition; 
        attribute vec3 vertexColor;    
        attribute vec3 vertexNormal;

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        uniform vec4 lightViewPosition;

        varying vec3 fragmentColor;    

        void main() {
            vec4 viewPosition = modelViewMatrix * vec4(vertexPosition, 1.0);
            
            vec3 lightVector = normalize(lightViewPosition.xyz - viewPosition.xyz);

            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);

            //CALCULATE GOURARD DIFFUSE LIGHT
            float lightIntensity = max(dot(lightVector, transformedNormal), 0.0);

            gl_Position = projectionMatrix * viewPosition;
            fragmentColor = vertexColor * lightIntensity; 
        }
    </script>
    <!-- GOURARD SPECULAR (PHONG MODEL) VSHADER -->
    <script id="v-shader-gspec" type="x-shader/x-vertex"> precision mediump float;

        attribute vec3 vertexPosition; 
        attribute vec3 vertexColor;    
        attribute vec3 vertexNormal;

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        uniform vec4 lightViewPosition;

        varying vec3 fragmentColor;    

        void main() {

            vec4 viewPosition = modelViewMatrix * vec4(vertexPosition, 1.0);
            
            vec3 lightVector = normalize(lightViewPosition.xyz - viewPosition.xyz);

            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);
            
            //DIFFUSE LIGHT
            float lightIntensity = max(dot(lightVector, transformedNormal), 0.0);

            //SPECULAR LIGHT
            vec3 reflectionVector = reflect(-lightVector, transformedNormal);
            vec3 viewDirection = normalize(-viewPosition.xyz);
            float specularLight = max(dot(viewDirection, reflectionVector), 0.0);
            float specular = pow(specularLight, 10.0);

            vec3 specularColor = vec3(1.0,1.0,1.0);

            vec3 ambientColor = vec3(0.1,0.1,0.1);

            //LIGHT MODELLL
            vec3 lightModel = ambientColor + lightIntensity * vertexColor + specular * specularColor;

            gl_Position = projectionMatrix * viewPosition;
            fragmentColor = lightModel; 
        }
    </script>
    <!-- PHONG DIFFUSE VSHADER -->
    <script id="v-shader-phong-diff" type="x-shader/x-vertex"> precision mediump float;

        attribute vec3 vertexPosition; 
        attribute vec3 vertexColor;    
        attribute vec3 vertexNormal;

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        uniform vec4 lightViewPosition;

        varying vec3 fragmentColor;
        varying vec3 outLightVector;
        varying vec3 outTransformedNormal;
        varying vec3 outVertexColor;

        void main() {
            vec4 viewPosition = modelViewMatrix * vec4(vertexPosition, 1.0);
            
            vec3 lightVector = normalize(lightViewPosition.xyz - viewPosition.xyz);

            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);

            //DONT DO CALCULATIONS IN VSHADER, BUT SEND VARIABLES TO FSHADER
            outLightVector = lightVector;
            outTransformedNormal = transformedNormal;
            outVertexColor = vertexColor;

            gl_Position = projectionMatrix * viewPosition;
        }
    </script>
    <!-- PHONG SPECULAR (PHONG MODEL) VSHADER -->
    <script id="v-shader-phong-spec" type="x-shader/x-vertex"> precision mediump float;

        attribute vec3 vertexPosition; 
        attribute vec3 vertexColor;    
        attribute vec3 vertexNormal;

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        uniform vec4 lightViewPosition;

        varying vec3 outLightVector;
        varying vec3 outTransformedNormal;
        varying vec3 outVertexColor;
        varying vec3 outViewDirection;
        varying vec3 fragmentColor;    

        void main() {

            vec4 viewPosition = modelViewMatrix * vec4(vertexPosition, 1.0);

            vec3 viewDirection = normalize(-viewPosition.xyz);
            
            vec3 lightVector = normalize(lightViewPosition.xyz - viewPosition.xyz);

            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);

            //SEND ALL NEEDED VARIABLES TO FSHADER
            outLightVector = lightVector;
            outTransformedNormal = transformedNormal;
            outVertexColor = vertexColor;
            outViewDirection = viewDirection;
            
            gl_Position = projectionMatrix * viewPosition;
        }
    </script>
    <!-- NORMAL FRAGMENT SHADER (ALSO USED FOR BOTH GOURARD DIFF AND SPEC) -->
    <script id="f-shader" type="x-shader/x-fragment"> precision mediump float;

        varying vec3 fragmentColor;

        void main() {
            gl_FragColor = vec4(fragmentColor, 1.0);
        }
    </script>
    <!-- PHONG DIFFUSE FSHADER -->
    <script id="f-shader-phong-diff" type="x-shader/x-fragment"> precision mediump float;

        varying vec3 outLightVector;
        varying vec3 outTransformedNormal;
        varying vec3 outVertexColor;

        void main() {

            //CALCULATE LIGHT FROM INCOMING VARIABLES
            float lightIntensity = max(dot(normalize(outLightVector), normalize(outTransformedNormal)), 0.0);
            vec3 finalColor = lightIntensity * outVertexColor;

            gl_FragColor = vec4(finalColor, 1.0);
        }
    </script>
    <!-- PHONG SPECULAR (PHONG MODEL) FSHADER -->
    <script id="f-shader-phong-spec" type="x-shader/x-fragment"> precision mediump float;

        varying vec3 outLightVector;
        varying vec3 outTransformedNormal;
        varying vec3 outVertexColor;
        varying vec3 outViewDirection;

        void main() {
            vec3 normLightVector = normalize(outLightVector);
            vec3 normTransNormal = normalize(outTransformedNormal);
            vec3 normViewDirec = normalize(outViewDirection);

            //DIFFUSE LIGHT
            float lightIntensity = max(dot(normLightVector, normTransNormal), 0.0);

            //SPECULAR LIGHT
            vec3 reflectionVector = reflect(-normLightVector, normTransNormal);
            float specularLight = max(dot(normViewDirec, reflectionVector), 0.0);
            float specular = pow(specularLight, 10.0);

            vec3 ambientLight = vec3(0.1,0.1,0.1);
            vec3 specularColor = vec3(1.0,1.0,1.0);
        
            //LIGHT MODELLL
            vec3 lightModel = ambientLight + lightIntensity * outVertexColor + specular * specularColor;


            vec3 finalColor = lightModel;

            gl_FragColor = vec4(finalColor, 1.0);
        }
    </script>
  </head>
  <body>
    <canvas id="canvas" width="1200" height="600"></canvas>
  </body>
</html>